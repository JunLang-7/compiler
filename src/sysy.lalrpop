use crate::ast::*;
// lalrpop 里的约定
grammar;

// 约束 lexer 的行为
match {
  // 跳过空白符和注释
  r"\s*" => {},
  r"//[^\n\r]*[\n\r]*" => {},
  r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => {},
  // 剩下的情况采用默认方式处理
  _
}

// 定义 CompUnit, 其返回值类型为 String
// parser 在解析完成后的行为是返回 FuncDef 的值
pub CompUnit: CompUnit = <items: GlobalItem*> => CompUnit { <> };

GlobalItem: GlobalItem = {
  <decl: Decl> => GlobalItem::Decl(<>),
  <func_def: FuncDef> => GlobalItem::FuncDef(<>),
}

FuncDef: FuncDef = {
  <head: FuncDefHead> ")" <block: Block> => FuncDef {func_type: head.0, ident: head.1, func_f_parms: None, block},
  <head: FuncDefHead> <func_f_params: FuncFParms> ")" <block: Block> => FuncDef {func_type: head.0, ident: head.1, func_f_parms: Some(func_f_params), block},
}

FuncDefHead: (FuncType, String) = {
  "void" <ident: Ident> "(" => (FuncType::Void, ident),
  <t: BType> <ident: Ident> "(" => (FuncType::Int, ident),
}

FuncFParms: FuncFParms = <func_f_parm: FuncFParm> <mut func_f_parms: ("," <FuncFParm>)*> => {
  func_f_parms.insert(0, func_f_parm);
  FuncFParms { func_f_parms }
};

FuncFParm: FuncFParm = {
  <b_type: BType> <ident: Ident> => FuncFParm { b_type, ident, dims: None },
  <b_type: BType> <ident: Ident> "[" "]" <dims: ("[" <ConstExp> "]")*> => FuncFParm { b_type, ident, dims: Some(dims) },
}

FuncRParms: FuncRParms = <exp: Exp> <mut exps: ("," <Exp>)*> => {
  exps.insert(0, exp);
  FuncRParms { exps }
};

Block: Block = "{" <block_items: BlockItem*> "}" => Block { <> };

BlockItem: BlockItem = {
  <decl: Decl> => BlockItem::Decl(<>),
  <stmt: Stmt> => BlockItem::Stmt(<>),
}

Stmt: Stmt = {
  OpenStmt,
  CloseStmt,
}

CloseStmt: Stmt = {
  "return" <exp: (Exp)?> ";" => Stmt::Return(exp),
  <lval: LVal> "=" <exp: Exp> ";" => Stmt::LValAssign(LValAssign { lval, exp }),
  <block: Block> => Stmt::Block(<>),
  <exp: (Exp)?> ";" => Stmt::Exp(<>),
  "if" "(" <cond: Exp> ")" <then_stmt: CloseStmt> "else" <else_stmt: CloseStmt> => Stmt::If(If { cond, then_stmt: Box::new(then_stmt), else_stmt: Some(Box::new(else_stmt)) }),
  "while" "(" <cond: Exp> ")" <body: CloseStmt> => Stmt::While(While { cond, body: Box::new(body) }),
  "break" ";" => Stmt::Break(Break),
  "continue" ";" => Stmt::Continue(Continue),
}

OpenStmt: Stmt = {
  "if" "(" <cond: Exp> ")" <then_stmt: Stmt> => Stmt::If(If { cond, then_stmt: Box::new(then_stmt), else_stmt: None }),
  "if" "(" <cond: Exp> ")" <then_stmt: CloseStmt> "else" <else_stmt: OpenStmt> => Stmt::If(If { cond, then_stmt: Box::new(then_stmt), else_stmt: Some(Box::new(else_stmt)) }),
  "while" "(" <cond: Exp> ")" <body: OpenStmt> => Stmt::While(While { cond, body: Box::new(body) }),
}

Exp: Exp = <lor_exp: LOrExp> => Exp { <> };

PrimaryExp: PrimaryExp = {
  "(" <exp: Exp> ")" => PrimaryExp::Exp(Box::new(exp)),
  Number => PrimaryExp::Number(<>),
  LVal => PrimaryExp::LVal(Box::new(<>)),
}

Number: i32 = <num: IntConst> => <>;

UnaryExp: UnaryExp = {
  <primary_exp: PrimaryExp> => UnaryExp::PrimaryExp(<>),
  <unary_op: UnaryOp> <unary_exp: UnaryExp> => UnaryExp::UnaryOp { op: unary_op, exp: Box::new(unary_exp) },
  <ident: Ident> "(" <func_r_parms: (FuncRParms)?> ")" => UnaryExp::FuncCall(FuncCall {
    ident,
    func_r_parms,
  }),
}

UnaryOp: UnaryOp = {
  "+" => UnaryOp::Plus,
  "-" => UnaryOp::Minus,
  "!" => UnaryOp::Not,
}

MulExp: MulExp = {
  <unary_exp: UnaryExp> => MulExp::UnaryExp(<>),
  <mul_exp: MulExp> <mul_op: MulOp> <unary_exp: UnaryExp> => MulExp::MulOp {
    lhs: Box::new(mul_exp),
    op: mul_op,
    rhs: Box::new(unary_exp),
  },
}

AddExp: AddExp = {
  <mul_exp: MulExp> => AddExp::MulExp(<>),
  <add_exp: AddExp> <add_op: AddOp> <mul_exp: MulExp> => AddExp::AddOp {
    lhs: Box::new(add_exp),
    op: add_op,
    rhs: Box::new(mul_exp),
  },
}

MulOp: MulOp = {
  "*" => MulOp::Mul,
  "/" => MulOp::Div,
  "%" => MulOp::Mod,
}

AddOp: AddOp = {
  "+" => AddOp::Plus,
  "-" => AddOp::Minus,
}

RelExp: RelExp = {
  <add_exp: AddExp> => RelExp::AddExp(<>),
  <rel_exp: RelExp> <rel_op: RelOp> <add_exp: AddExp> => RelExp::RelOp {
    lhs: Box::new(rel_exp),
    op: rel_op,
    rhs: Box::new(add_exp),
  },
}

EqExp: EqExp = {
  <rel_exp: RelExp> => EqExp::RelExp(<>),
  <eq_exp: EqExp> <eq_op: EqOp> <rel_exp: RelExp> => EqExp::EqOp {
    lhs: Box::new(eq_exp),
    op: eq_op,
    rhs: Box::new(rel_exp),
  },
}

LAndExp: LAndExp = {
  <eq_exp: EqExp> => LAndExp::EqExp(<>),
  <land_exp: LAndExp> <land_op: LAndOp> <eq_exp: EqExp> => LAndExp::LAndOp {
    lhs: Box::new(land_exp),
    rhs: Box::new(eq_exp),
  },
}

LOrExp: LOrExp = {
  <land_exp: LAndExp> => LOrExp::LAndExp(<>),
  <lor_exp: LOrExp> <lor_op: LOrOp> <land_exp: LAndExp> => LOrExp::LOrOp {
    lhs: Box::new(lor_exp),
    rhs: Box::new(land_exp),
  },
}

RelOp: RelOp = {
  "<" => RelOp::Lt,
  ">" => RelOp::Gt,
  "<=" => RelOp::Le,
  ">=" => RelOp::Ge,
}

EqOp: EqOp = {
  "==" => EqOp::Eq,
  "!=" => EqOp::Ne,
}

LAndOp: LAndOp = "&&" => LAndOp::And;
LOrOp: LOrOp = "||" => LOrOp::Or;

Decl: Decl ={
  <const_decl: ConstDecl> => Decl::ConstDecl(<>),
  <var_decl: VarDecl> => Decl::VarDecl(<>),
}

ConstDecl: ConstDecl = "const" <b_type: BType> <const_def: ConstDef> <mut const_defs: ("," <ConstDef>)*> ";" => {
  const_defs.insert(0, const_def);
  ConstDecl { b_type, const_defs }
};

BType: BType = "int" => BType::Int;

ConstDef: ConstDef = {
  <ident: Ident> <dims: ("[" <ConstExp> "]")*> => ConstDef { ident, dims, const_init_val: ConstInitVal::Exp(ConstExp { exp: Exp::number(0) }) },
  <ident: Ident> <dims: ("[" <ConstExp> "]")*> "=" <const_init_val: ConstInitVal> => ConstDef { <> },
}

ConstInitVal: ConstInitVal = {
  <const_exp: ConstExp> => ConstInitVal::Exp(<>),
  "{" "}" => ConstInitVal::List(Vec::new()),
  "{" <init: ConstInitVal> <mut inits: ("," <ConstInitVal>)*> "}" => {
    inits.insert(0, init);
    ConstInitVal::List(inits)
  },
}

LVal: LVal = <ident: Ident> <indices: ("[" <Exp> "]")*> => LVal { <> };

ConstExp: ConstExp = <exp: Exp> => ConstExp { <> };

VarDecl: VarDecl = <b_type: BType> <var_def: VarDef> <mut var_defs: ("," <VarDef>)*> ";" => {
  var_defs.insert(0, var_def);
  VarDecl { b_type, var_defs }
};

VarDef: VarDef = {
  <ident: Ident> <dims: ("[" <ConstExp> "]")*> => VarDef { ident, dims, init_val: None },
  <ident: Ident> <dims: ("[" <ConstExp> "]")*> "=" <init_val: InitVal> => VarDef { ident, dims, init_val: Some(init_val) },
}

InitVal: InitVal = {
  <exp: Exp> => InitVal::Exp(<>),
  "{" "}" => InitVal::List(Vec::new()),
  "{" <init: InitVal> <mut inits: ("," <InitVal>)*> "}" => {
    inits.insert(0, init);
    InitVal::List(inits)
  },
}

// 如果匹配到标识符, 就返回这个字符串
// 一对尖括号在此处指代的是正则表达式匹配到的字符串 (&str)
// 关于尖括号到底代表什么, 请 RTFM
Ident: String = r"[_a-zA-Z][_a-zA-Z0-9]*" => <>.to_string();

// 对整数字面量的处理方式: 把匹配到的字符串按对应进制转换成数字
IntConst: i32 = {
  r"[1-9][0-9]*" => i32::from_str_radix(<>, 10).unwrap(),
  r"0[0-7]*" => i32::from_str_radix(<>, 8).unwrap(),
  r"0[xX][0-9a-fA-F]+" => i32::from_str_radix(&<>[2..], 16).unwrap(),
}
